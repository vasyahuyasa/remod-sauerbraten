//  HERE IS COMMON FUNCTIONS AND VARIABLES

//-------- CONSTANTS --------//

//IRC colors
ircgreen    = [ result (concatword "g" $arg1) ] //green    
ircblue     = [ result (concatword "b" $arg1) ] //blue     
ircyellow   = [ result (concatword "y" $arg1) ] //yellow   
ircred      = [ result (concatword "r" $arg1) ] //red      
ircgrey     = [ result (concatword "a" $arg1) ] //grey     
ircmagenta  = [ result (concatword "m" $arg1) ] //magenta  
ircorange   = [ result (concatword "o" $arg1) ] //orange   
irccyan     = [ result (concatword "c" $arg1) ] //cyan     
ircviolet   = [ result (concatword "v" $arg1) ] //violet   
ircpurple   = [ result (concatword "p" $arg1) ] //purple   
ircbrown    = [ result (concatword "n" $arg1) ] //brown   
ircdgrey	= [ result (concatword "d" $arg1) ] //dark grey
ircreset	= [ result (concatword "u" $arg1) ] //reset to normal color

//Server colors
green 	= [ result (concatword "^f0" $arg1) ] // green: player talk
blue 	= [ result (concatword "^f1" $arg1) ] // blue: "echo" command
yellow 	= [ result (concatword "^f2" $arg1) ] // yellow: gameplay messages
red 	= [ result (concatword "^f3" $arg1) ] // red: important errors
gray 	= [ result (concatword "^f4" $arg1) ]
magenta	= [ result (concatword "^f5" $arg1) ]
orange	= [ result (concatword "^f6" $arg1) ]
white 	= [ result (concatword "^f7" $arg1) ]

//Server staff
MODENAMES = "ffa coop teamplay insta instateam effic efficteam tac tacteam capture regencapture ctf instactf protect instaprotect hold instahold efficctf efficprotect effichold collect instacollect efficcollect"

TEAMMODES = "teamplay instateam efficteam tacteam capture regencapture ctf instactf efficctf protect instaprotect efficprotect hold instahold effichold instacolelct efficcollect"

FLAGMODES = "ctf instactf efficctf protect instaprotect efficprotect hold instahold effichold"

CTFMODES = "ctf instactf efficctf"

PRIVNAMES = "none master master admin"

//-------- SOME USEFUL FUNCTIONS --------//

//////////////////
// Convert mode number to string. Uses $MODENAMES
// @arg1 mode number
// @group server
// @return mode name
// @example (modetostr 0) //returns ffa
modetostr = [
	result (at $MODENAMES $arg1)
]
//////////////////
// Checks if list contains specified item
// @group server
// @arg1 List
// @arg2 item to check
// @return 1 if list contains the item, otherwise 0
// @example isinlist "1 2 3" "3" //returns 1
isinlist = [ 
	result (!= (indexof $arg1 $arg2) -1) 
]

//////////////////
// Returns list of client numbers of all connected players
// @group player
// @return list of cn
allplayers = [ 
	result (listclients) 
]

//////////////////
// Returns client numbers of all active players (not spectators)
// @group player
// @return list of players' cn
allactiveplayers = [
	all = (listclients)
	res = ""
	looplist cn $all [
		if (&& (= (playerexists $cn) 1) (= (isspectator $cn) 0)) [
			res = (concat $res $cn)
		]
	]
	result $res
]

//////////////////
// Returns KpD (kills per deathes) of player
// @group player
// @arg1 cn of player
// @return float number
getkpd = [
	deathes = (getdeaths $arg1)
	frags = (getfrags $arg1)
	result (? $deathes (divf (div (* 10 $frags) $deathes) 10) (? $frags $frags 0))
]

//////////////////
// Returns formatted and colored "usage" string for specified command
// @group server
// @arg1 command name
// @return usage  string 
usage = [ 
	result (concatword "^f1Usage: ^f0" $commandchar (commandhelp $arg1))
]

//////////////////
// Prints specified string to output (log) with timestamp
// @group server
// @arg1 string
// @return [timestamp] string  
log = [
	echo (format "[%1] %2" (systimef "%Y-%m-%d %X") $arg1)
]

//////////////////
// Prints error message to output (log) with timestamp
// @group server
// @arg1 string
// @example log_error "Could not create DB connection"  
log_error = [
	echo (format "[%1] [ERROR] %2" (systimef "%Y-%m-%d %X") $arg1)
]

//////////////////
// Prints warning message to output (log) with timestamp
// @group server
// @arg1 string
// @example log_warn "SQLite could be wery slow on module 'spy', please use MySQL instead"  
log_warn = [
	echo (format "[%1] [WARN] %2" (systimef "%Y-%m-%d %X") $arg1)
]

//////////////////
// Prints info message to output (log) with timestamp
// @group server
// @arg1 string
// @example log_info "Make install, not war"
log_info = [
	echo (format "[%1] [INFO] %2" (systimef "%Y-%m-%d %X") $arg1)
]

//////////////////
// Execute all .cfg files in directory
// @group server
// @arg1 directory
execdir = [
	dir = $arg1
	i = 0
	loopfiles f $dir cfg [		
		file = (format "%1/%2.cfg" $dir $f) 
		//log (format "executing '%1'" $file)
		exec $file
		i = (+ $i 1)
 	]
	log_info (format "executed %1 files in '%2'" $i $dir)		
]

//////////////////
// Convert milliseconds to time string
// @group server
// @arg1 milliseconds
// @return %d days %h hours %m minutes
millistostr = [
	res = ""
	if (>= (div $arg1 86400000) 1) [
		res = (formatmillis "%d days %h hours %m minutes" $arg1)
	] [
		if (>= (div $arg1 3600000) 1) [
			res = (formatmillis "%h hours %m minutes" $arg1)
		] [
			if (>= (div $arg1 60000) 1) [
				res = (formatmillis "%m minutes" $arg1)	
			]
		]
	]
	result $res
]

//////////////////
// Initialize vaiable $arg1 with default value $arg2 if it's not initialized yet
// @group server
// @arg1 vaiable
// @arg2 default value
// @example defaultvalue "var_name" "value" // if there is no $var_name before this call, does $var_name = "value" 
defaultvalue = [
	if (=s "" (getalias $arg1)) [
		alias $arg1 $arg2
	] 
]

//////////////////
// Prettify escaped list (add commas) with unescaped list items
// @group server
// @arg1 escaped list
// @example prettyescapedlist "|RB|degrave |RB|^^o_o^^ ^"strangenickname^"^^" // returns  |RB|degrave, |RB|^o_o^, "strangenickname"^
prettyescapedlist = [
	res = ""
	first = 1
	log_info $arg1
	looplist v $arg1 [
		if $first [
			first = 0
			res = (unescape $v)
		] [
			res = (concatword $res ", " (unescape $v))
		]
	]
	result $res
]

//////////////////
// Concatenate string with commas 
// @group server
// @arg1 string
// @arg2 new item
// @example concatwithcommas "item1" "teim2" // returns "item1, item2"
concatwithcommas = [
	res = ""
	if (=s "" $arg1) [
		res = $arg2
	] [
		res = (concatword $arg1 ", " $arg2)
	]
	result $res
]

//////////////////
// Deprecated set client privelege
// @group server
// @arg1 cn
// @arg2 priv
setmaster = [
	cn = $arg1
	val = $arg2
	
	log_warn "setmaster is deprecated, please use setpriv instead"
	setpriv $cn $val
]

//////////////////
// Deprecated set client privelege
// @group player
// @arg1 priv
// @return privelege name
privname = [ result (at $PRIVNAMES $arg1) ]

//////////////////
// Check if server in team mode
// @group server
// @return 1 if team mode, otherwise 0
isteammode = [ if (= (isinlist $TEAMMODES (modetostr (getmode))) 1) [ result 1 ] [ result 0 ] ]

//////////////////
// Check if server in flag mode
// @group server
// @return 1 if team mode, otherwise 0
isflagmode = [ if (= (isinlist $FLAGMODES (modetostr (getmode))) 1) [ result 1 ] [ result 0 ] ]

//////////////////
// Check if server in ctf mode
// @group server
// @return 1 if team mode, otherwise 0
isctfmode = [ if (= (isinlist $CTFMODES (modetostr (getmode))) 1) [ result 1 ] [ result 0 ] ]

// remodex
// convert gun number to name
gunname = [
	_gunname = "Unknown"
	case $arg1 0 [
		_gunname = "CS"
	] 1 [
		_gunname = "SG"
	] 2 [
		_gunname = "CG"
	] 3 [
		_gunname = "RL"
	] 4 [
		_gunname = "RI"
	] 5 [
		_gunname = "GL"
	] 6 [
		_gunname = "PI"
	]

	result $_gunname
]
